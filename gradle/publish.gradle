
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: rootProject.file('gradle/platform.gradle')

def platform = platformOf(project)


publishing {
    repositories {
        maven {
            if (publishLocal) {
                url globalM2
            } else {
                url 'https://dl.bintray.com/kotlin/ktor'
            }
        }
    }

    publications {
        mavenProject(MavenPublication) {
            if (platform != 'native') {
                from components.java
            }
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourceJar

            if (!skipDokka) {
                artifact javadocJar {
                    classifier 'javadoc'
                }

                artifact dokkaJar {
                    classifier 'kdoc'
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publish = true
    override = true

    pkg {
        userOrg = 'kotlin'
        repo = 'ktor'
        name = 'ktor'

        publications = ['mavenProject']
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ktorio/ktor'

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

bintrayUpload.doFirst {
    publications = project.publishing.publications
}

def skipUpload = project.path.contains("ktor-samples")
if (!skipUpload) {
    apply from: rootProject.file("gradle/bintray.gradle")
}
